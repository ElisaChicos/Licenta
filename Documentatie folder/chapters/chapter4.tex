\lstset{frame=tb,
	language=C++,
	aboveskip=3mm,
	belowskip=3mm,
	showstringspaces=false,
	columns=flexible,
	basicstyle={\small\ttfamily},
	numbers=none,
	numberstyle=\tiny,
	breaklines=true,
	breakatwhitespace=true,
	tabsize=3}

\chapter{Detalii de implementare}
În acest capitol voi prezenta detaliile de implementare a algoritmului cât și modul în care am demonstrat corectitudinea codului utilizând Dafny.
\section{Reprezentarea datelor de intrare și a celor de ieșire}

\subsection{Varibilele folosite, tipurile și semnificația acestora}

\begin{itemize}
	\item suma : $int$ $\rightarrow$ reprezintă suma pe care trebuie să o plătim 
	\item rest : $int$ $\rightarrow$ reprezintă suma care ne-a rămas de plătit după ce am ales o bancnotă
	\item solutieFinala : $seq<int>$ $\rightarrow$ reprezintă soluția finală a problemei 
	\item solutieCurenta : $seq<int>$ $\rightarrow$ reprezintă soluția creată până la pasul curent
	\item solutieOarecare  : $seq<int>$ $\rightarrow$ reprezintă o soluție pe care am folosit-o pentru a demonstra corectitudinea programului
\end{itemize}

\subsection{Descrierea soluției rezultate}
Bancnotele folosite de mine pentru rezolvarea acestei probleme sunt $B = \{1, 5, 10, 20, 50\}$, astfel, soluția problemei va fi o secvență de numere naturale $ soluție = \{n_1,n_5,n_{10},n_{20},n_{50} \}$ unde $n_i$ va numărul de $i$ bancnote folosite iar 
\begin{center}
	$n_1 \cdot 1 + n_5 \cdot 5 + n_{10} \cdot 10 + n_{20} \cdot 20 + n_{50} \cdot 50 == suma $.
\end{center}
\vspace{0.5cm}


\section{Implementarea algoritmului Greedy}
\subsection{Predicate și funcții }
Înainte de a trece la implementarea propriu-zisă a algoritmului, o să încep prin a prezenta predicatele care asigură corectitudinea rezultatului întors de către metoda ce generează soluția optimă pentru suma pe care trebuie să o plătim. \par 
Predicatele sunt metode care returnează o valoare de adevăr și sunt folosite pentru a verifică o anumită proprietate sau mai multe prin intermediul unei singure instrucțiuni.
\par
În Dafny, funcțiile sunt asemănătoare cu funcțiile matematice. Funcțiile conțin o singură expresie care returnează o valoare de tipul declarat in antetul funcției.


\par
\begin{enumerate}
	\item Predicatul $esteSolutieValida(solutie : seq<int>)$ $\rightarrow$ acest predicat ne asigură că soluția primită ca parametru este de lungime 5 și că fiecare element este mai mare sau egal cu 0. Condiții menționate anterior sunt foarte importante deoarece, având 5 bancnote (1, 5, 10, 20, 50), înseamnă că va trebuie să existe un elemnt al soluției pentru fiecare dintre ele.
	\par 
\begin{lstlisting}
	predicate esteSolutieValida(solutie : seq<int>)
	{
		|solutie| == 5 && solutie[0] >= 0 && solutie[1] >= 0 && 	solutie[2] >= 0 && solutie[3] >=0 && solutie[4] >=0
	}
\end{lstlisting}
\item Predicatul $esteSolutie(solutie : seq<int>, suma : int) \rightarrow$ acest predicat verifică faptul că suma elementelor secvenței $soluție$ înmulțite cu bancnotele folosite în problemă este egală cu $suma$. Dacă cele două sunt egale, atunci secvență $soluție$ devine soluție posibilă pentru $suma$.
\begin{lstlisting}
	predicate esteSolutie(solutie : seq<int>, suma : int)
		requires esteSolutieValida(solutie)
	{
		solutie[0] * 1 + solutie[1] * 5 + solutie[2] * 10 + solutie[3] * 20 + solutie[4] * 50 == suma
	}
\end{lstlisting}
\vspace{1.5cm}
\item Funcția $cost(solutie : seq<int>) : int \rightarrow$ această funcție calculează și returnează câte bancnote au fost folosite pentru a formă soluția posibilă memorată în variabila $solutie$ dată ca parametru.
\begin{lstlisting}
	function cost(solutie : seq<int>) : int 
		requires esteSolutieValida(solutie)
	{
		solutie[0] + solutie[1] + solutie[2] + solutie[3] + solutie[4]
	}
\end{lstlisting}

\item Predicatul $ esteSolutieOptima(solutie : seq<int>, suma : int) \rightarrow$ acest predicat va returnat valoarea de adevăr dacă soluția primită ca parametru este o soluție posibilă pentru suma pe  care trebuie să o plătim și orice altă soluție posibilă are costul mai mare sau egal. Acest lucru se întâmplă deoarece Problema Bancnotelor este o problemă de minimizare iar soluția trebuie să aibe costul minim.
\begin{lstlisting}
	predicate esteSolutieOptima(solutie : seq<int>, suma : int)
		requires esteSolutieValida(solutie)
	{   
		esteSolutie(solutie, suma) &&
		forall solutieOarecare :: esteSolutieValida(solutieOarecare) && esteSolutie(solutieOarecare, suma) 
		==> cost(solutieOarecare) >= cost(solutie)
	}
\end{lstlisting}

\item Predicatul $  INV(rest : int, suma : int, solutieFinala : seq<int>) \rightarrow$ acest predicat va fi adevărat dacă orice soluție care este validă este și soluție posibilă pentru $rest$ (suma care ne-a rămas de plătit) atunci suma dintre această soluție și cea dată ca parametru trebuie să fie soluție posibilă pentru suma totală care trebuie plătită. Similar se întâmplă și pentru a demosntra că suma celor două soluții este soluție optimă pentru $suma$.
\begin{lstlisting}
	predicate INV(rest : int, suma : int, solutieFinala : seq<int>)
		requires esteSolutieValida(solutieFinala)
	{
		forall solutieCurenta :: esteSolutieValida(solutieCurenta) ==>
		(esteSolutie(solutieCurenta, rest) ==> 
		esteSolutie([solutieFinala[0] + solutieCurenta[0], solutieFinala[1] + solutieCurenta[1], 
		solutieFinala[2] + solutieCurenta[2], solutieFinala[3] + solutieCurenta[3], solutieFinala[4] + solutieCurenta[4]], suma)) &&
		(esteSolutieOptima(solutieCurenta, rest) ==> 
		esteSolutieOptima([solutieFinala[0] + solutieCurenta[0], solutieFinala[1] + solutieCurenta[1],
		solutieFinala[2] + solutieCurenta[2], solutieFinala[3] + solutieCurenta[3], solutieFinala[4] + solutieCurenta[4]], suma))
	}
\end{lstlisting}
\end{enumerate}




\subsection{Algoritmul Greedy}
\begin{lstlisting}
method nrMinimBancnote(suma : int) returns (solutie : seq<int>)
	requires suma >= 0
	ensures esteSolutieValida(solutie)
	ensures esteSolutie(solutie, suma)
	ensures esteSolutieOptima(solutie, suma)
{
  var rest  :=  suma;
  var s1 := 0;
  var s5 := 0;
  var s10 := 0;
  var s20 := 0;
  var s50 := 0;
  while(rest > 0)
  	   decreases rest
   	invariant 0 <= rest <= suma
   	invariant esteSolutie([s1, s5, s10, s20, s50], suma - rest)
   	invariant INV(rest, suma, [s1, s5, s10, s20, s50])
  {
 	var i := 0;
	var s := gasireMaxim(rest);
	if( s == 1)
	{
	   cazMaxim1(rest, suma, [s1, s5, s10, s20, s50]);
	   s1 := s1 + 1;
	   assert esteSolutie([s1, s5, s10, s20, s50], suma - (rest - 1));
	   assert INV(rest - 1, suma, [s1, s5, s10, s20, s50]);
	}
	else if(s == 5)
	{
	   cazMaxim5(rest, suma, [s1, s5, s10, s20, s50]);
	   s5 := s5 + 1;
	   assert esteSolutie([s1, s5, s10, s20, s50], suma - (rest - 5));
	   assert INV(rest - 5, suma, [s1, s5, s10, s20, s50]);
			
	}
	else if (s == 10)
	{
	   cazMaxim10(rest, suma, [s1, s5, s10, s20, s50]);
	   s10 := s10 + 1;
	   assert esteSolutie([s1, s5, s10, s20, s50],
	   		 suma - (rest - 10));
	   assert INV(rest - 10, suma, [s1, s5, s10, s20, s50]);	
	}
	else if(s == 20)
	{
	   cazMaxim20(rest, suma, [s1, s5, s10, s20, s50]);
	   s20 := s20 + 1;
	   assert esteSolutie([s1, s5, s10, s20, s50], suma - (rest - 20));
       assert INV(rest - 20, suma, [s1, s5, s10, s20, s50]);
	}
	else
	{
	   cazMaxim50(rest, suma, [s1, s5, s10, s20, s50]);
	   s50 := s50 + 1;
	   assert esteSolutie([s1, s5, s10, s20, s50], suma - (rest - 50));
	   assert INV(rest - 50, suma, [s1, s5, s10, s20, s50]);
	}
	  rest := rest - s;
	}
	solutie := [s1, s5, s10, s20, s50];
}
\end{lstlisting}
\par 
Algoritmul propus de mine primește ca parametru suma pe care va trebui să o plătim și returnează o soluție care respectă proprietățile discutate mai sus în secțiunea $4.1.2 \ Descrierea \ \ soluției \ \ rezultate $.
\par 
Preconditiile sunt expresii booleene care trebuie să fie adevărate pentru variabilele date ca parametru, în timp ce postconditiile trebuie să fie adevărate pentru variabilele returnate de metodă. Acestea sunt adăugate la începutul metodei pentru a determina o bună funcționare a programului și totodată corectitudinea acestuia.
\par 
Preconditia $requires \ \ suma >= 0$ ne indică faptul că suma trebuie să fie mai mare sau egală cu 0 pentru a se putea realiza metoda.\par 
\vspace{0.5cm}
\begin{lstlisting}
	ensures esteSolutieValida(solutie) 
	ensures esteSolutie(solutie, suma)
	ensures esteSolutieOptima(solutie, suma)
\end{lstlisting}
 		

\par
\vspace{0.25cm}
Aceste trei postconditii se focusează pe soluția returnată de metodă. Soluția trebuie să fie valida, să fie o soluție posibilă pentru suma care trebuie plătită și totodată să fie și soluție oprimă pentru aceasta.
\par 
După îndeplinirea cu succes a preconditiilor și a postconditiilor cerute, vom intra în corpul metodei. \par 
La început, vom declara 5 variabile $s_1, s_5 , s_{10}, s_{20}, s_{50}$ pe care le inițializăm cu valoarea 0. Aceste variabile vor memora câte bancnote de fiecare tip sunt folosite. \par 
În bucla $while$ se va calcula, la fiecare pas, cu ajutorul uneii metodei $gasireMaxim(rest)$ valoarea bancnotei maxime care este mai mică sau egală cu $rest$. În funcție de valoarea returnată de această metodă, se va incrementa valoarea variabilei corespunzătoare declarată la începutul metodei, va scădea valoarea variabilei $rest$ cu valoarea bancnotei alese la pasul curent și se va apela o anumită lemma care ajută la demonstrarea corectitudinii programului însă voi reveni ulterior la acest aspect.
\par 

Un alt lucru interesant la acest limbaj de programare este prezența invariantilor la începutul buclelor. Invarianții sunt, asemenea postconditiilor și preconditiilor, proprietăți care trebuie respectate pentru a se realiza cu succes bucla while.

\vspace{0.5cm}
\fbox{\parbox{5.5in}{
		decreases rest \par 
		invariant 0 $<=$ rest $<=$ suma \par 
		invariant esteSolutie([s1, s5, s10, s20, s50], suma - rest) \par
		invariant INV(rest, suma, [s1, s5, s10, s20, s50])
	}
}
\par 
\vspace{0.5cm}
Acești invarianti ne indică faptul că $rest$ își va schimba valoarea și va descrește dar va fi mereu mai mare decât 0 și mai mică sau egală decât $suma$ și că soluția creată până la pasul curent este o soluție posibilă pentru $suma-rest$. 
Totodată, vom folosi predicatul $INV$ pentru a verifica fapul că soluția noastră este soluție optimă. 
\par
\subsection{Leme importante în demonstrarea corectitudinii}
Lemele sunt metode care sunt folosite pentru a verifica corectitudinea programului. Acestea conțin precondiții iar proprietatea care trebuie demonstrată va fi postconditia lemei.\par
Uneori, lemele pot fi demonstrate de către Dafny fără a adăuga instrucțiuni, ceea ce înseamnă că un corp fără instrucțiuni servește că argument pentru demonstrație.
\par
În prezentarea agloritmului Greedy creat de mine, în funcție de bancnotă aleasă, va fi apelată o lema care va verifica dacă alegerea făcută va duce la o soluție optimă. Spre exemplu, pentru $s = 1$, unde $s$ este valoarea returnată de metoda $gasireMaxim(rest)$, se va apela lema $cazMaxim1$, pentru $s = 5$ se va apela lema $cazMaxim5$, iar acest lucru se întâmplă pentru fiecare bancnotă existentă în algoritm. \par 
În continuare, o să prezint lemele $cazMaxim1$ și $cazMaxim50$. Primele patru leme, $cazMaxim1$, $cazMaxim5$, $cazMaxim10$, $cazMaxim20$, sunt similare, drep urmare voi prezenta doar lema $cazMaxim1$. Însă, în subsecțiunea următoare, voi reveni asupra lemelor $cazMaxim10$, $cazMaxim20$ deoarece pentru demonstrarea corectitudinii acestor cazuri a fost nevoie de o abordare diferită.
\vspace{2.5cm}
\begin{enumerate}
\item Lema cazMaxim1(rest : int, suma : int, solutieFinala : seq$<$int$>$) 
\par
\begin{lstlisting}
	lemma cazMaxim1(rest : int, suma : int, solutieFinala : seq<int>)
		requires rest < 5
		requires esteSolutieValida(solutieFinala)
		requires INV(rest, suma, solutieFinala)
		ensures INV(rest-1, suma, [solutieFinala[0] + 1, solutieFinala[1], solutieFinala[2], solutieFinala[3], solutieFinala[4]])
	{
		
		forall solutieCurenta | esteSolutieValida(solutieCurenta) && esteSolutieOptima(solutieCurenta, rest - 1) 
		ensures esteSolutieOptima([solutieFinala[0] + solutieCurenta[0] + 1, solutieFinala[1] + solutieCurenta[1],
		solutieFinala[2] + solutieCurenta[2], solutieFinala[3] + solutieCurenta[3], solutieFinala[4] + solutieCurenta[4]], suma)
		{
			assert esteSolutie(solutieCurenta, rest - 1);
			assert esteSolutie([solutieCurenta[0] + 1, solutieCurenta[1], solutieCurenta[2], solutieCurenta[3], solutieCurenta[4]], rest);
			
			assert forall solutieOarecare :: esteSolutieValida(solutieOarecare) && esteSolutie(solutieOarecare, rest - 1) 
			==> cost(solutieOarecare) >= cost(solutieCurenta);
			
			assert esteSolutie([solutieFinala[0] + solutieCurenta[0] + 1, solutieFinala[1] + solutieCurenta[1],
			solutieFinala[2] + solutieCurenta[2], solutieFinala[3] + solutieCurenta[3], solutieFinala[4] + solutieCurenta[4]], suma);
			
			assert forall solutieOarecare :: esteSolutieValida(solutieOarecare) &&  esteSolutie(solutieOarecare, suma) 
			==> cost(solutieOarecare) >= cost([solutieCurenta[0] + solutieFinala[0] + 1, solutieCurenta[1] + solutieFinala[1],
			solutieCurenta[2] + solutieFinala[2], solutieCurenta[3] + solutieFinala[3], solutieCurenta[4] + solutieFinala[4]]);
		}
		
		assert forall solutieCurenta :: esteSolutieValida(solutieCurenta) 
		&& esteSolutieOptima(solutieCurenta, rest - 1) ==> 
		esteSolutieOptima([solutieFinala[0] + solutieCurenta[0] + 1, solutieFinala[1] + solutieCurenta[1],
		solutieFinala[2] + solutieCurenta[2], solutieFinala[3] + solutieCurenta[3], solutieFinala[4] + solutieCurenta[4]], suma);
	}
\end{lstlisting} 
\setlength{\parindent}{2em}
\par
Am creat un forall statement cu ajutorul căruia vom demonstra că dacă vom alege bancnota de valoare 1, vom crea o soluție oprimă pentru variabila $suma$.
\par
Acest lucru îl vom demonstra cu ajutorul assert-urilor. Cu ajutorul acestora vom verifică ce "știe" verificatorul Dafny și ce va trebui demonstrat separat cu ajutorul lemelor. 
\par
Forall statement-ul despre care am amintit mai sus va crea o soluție numită $solutieCurenta$ despre care știm că este o soluție validă și că este o soluție oprimă pentru $rest-1$. 
Spre exemplu, dacă $rest = 4$, $suma = 54$ și $solutieFinala = [0,0,0,0,1]$ soluția aleasă de forall statement va fi $[3,0,0,0,0]$ care este soluție optimă pentru 3. \par 
Atfel, știind că $solutieCurenta$ este soluție optimă pentru $rest-1$, este evidet că aceasta este o soluție posibilă pentru $rest-1$. Vom încerca să modificăm $solutieCurenta[0]+1$, pentru a crea o soluție posibilă pentru $rest$.
\par 
Următorul pas este să formăm o nouă soluție prin adunarea celor două soluții cunoscute pentru a crea o soluție oprimă pentru variabila $suma$: $[solutieFinala[0] + solutieCurenta[0] + 1, solutieFinala[1] + solutieCurenta[1],solutieFinala[2] + \\ solutieCurenta[2], solutieFinala[3] + solutieCurenta[3], solutieFinala[4] +\\ solutieCurenta[4]]$.

\item Lema cazMaxim50(rest : int, suma : int, solutieFinala : seq$<$int$>$)
\par 
\begin{lstlisting}
lemma cazMaxim50(rest : int, suma : int, solutieFinala : seq<int>)
	requires rest >= 50
	requires esteSolutieValida(solutieFinala)
	requires INV(rest, suma, solutieFinala)
	ensures INV(rest - 50, suma, [solutieFinala[0], solutieFinala[1], solutieFinala[2], solutieFinala[3], 1 + solutieFinala[4]])
{
	assert forall solutieCurenta :: esteSolutieValida(solutieCurenta) ==>
	(esteSolutie(solutieCurenta, rest) ==> 
	esteSolutie([solutieFinala[0] + solutieCurenta[0], solutieFinala[1] + solutieCurenta[1], solutieFinala[2] + solutieCurenta[2], solutieFinala[3] + solutieCurenta[3],solutieFinala[4] + solutieCurenta[4]], suma));
	
	forall solutieCurenta | esteSolutieValida(solutieCurenta) 
	&& esteSolutie(solutieCurenta, rest - 50) 
		ensures esteSolutie([solutieFinala[0] + solutieCurenta[0], solutieFinala[1] + solutieCurenta[1], solutieFinala[2] + solutieCurenta[2], solutieFinala[3] + solutieCurenta[3], 1 + solutieFinala[4] + solutieCurenta[4]], suma)
	{
		assert esteSolutie([solutieCurenta[0], solutieCurenta[1], solutieCurenta[2], solutieCurenta[3], 1 + solutieCurenta[4]], rest);
	}
	forall solutieCurenta | esteSolutieValida(solutieCurenta) 
	&& esteSolutieOptima(solutieCurenta, rest - 50) 
		ensures esteSolutieOptima([solutieFinala[0] + solutieCurenta[0], solutieFinala[1] + solutieCurenta[1], solutieFinala[2] + solutieCurenta[2], solutieFinala[3] + solutieCurenta[3], 1 + solutieFinala[4] + solutieCurenta[4]], suma)
	{
		assert esteSolutie(solutieCurenta, rest - 50);
		assert esteSolutie([solutieCurenta[0], solutieCurenta[1], solutieCurenta[2], solutieCurenta[3], 1 + solutieCurenta[4]], rest);
		
		assert forall solutieOarecare :: esteSolutieValida(solutieOarecare)
		&& esteSolutie(solutieOarecare, rest - 50) 
		==> cost(solutieOarecare) >= cost(solutieCurenta);
		
		assert esteSolutie([solutieFinala[0] + solutieCurenta[0], solutieFinala[1] + solutieCurenta[1], solutieFinala[2] + solutieCurenta[2], solutieFinala[3] + solutieCurenta[3], 1 + solutieFinala[4] + solutieCurenta[4]], suma);
		
		forall solutieOarecare | esteSolutieValida(solutieOarecare)
		&& esteSolutie(solutieOarecare, suma)
			ensures cost(solutieOarecare) >= cost([solutieFinala[0] + solutieCurenta[0], solutieFinala[1] + solutieCurenta[1], solutieFinala[2] + solutieCurenta[2], solutieFinala[3] + solutieCurenta[3], 1 + solutieFinala[4] + solutieCurenta[4]])
		{
			solutieFianalaAreCostMinim(rest, suma, solutieOarecare, solutieFinala, solutieCurenta);
		}
		assert forall solutieOarecare :: esteSolutieValida(solutieOarecare)
		&& esteSolutie(solutieOarecare, suma) 
		==> cost(solutieOarecare) >= cost([solutieFinala[0] + solutieCurenta[0], solutieFinala[1] + solutieCurenta[1], solutieFinala[2] + solutieCurenta[2], solutieFinala[3] + solutieCurenta[3], 1 + solutieFinala[4] + solutieCurenta[4]]);
	}
	assert forall solutieCurenta :: esteSolutieValida(solutieCurenta)
	&& esteSolutieOptima(solutieCurenta, rest - 50) ==> 
	esteSolutieOptima([solutieFinala[0] + solutieCurenta[0], solutieFinala[1] + solutieCurenta[1], solutieFinala[2] + solutieCurenta[2], solutieFinala[3] + solutieCurenta[3], 1 + solutieFinala[4] + solutieCurenta[4]], suma);
	
	assert forall solutieCurenta :: esteSolutieValida(solutieCurenta) ==>
	(esteSolutie(solutieCurenta, rest - 50) ==> 
	esteSolutie([solutieFinala[0] + solutieCurenta[0], solutieFinala[1] + solutieCurenta[1], solutieFinala[2] + solutieCurenta[2], solutieFinala[3] + solutieCurenta[3], 1 + solutieFinala[4] + solutieCurenta[4]], suma));
	
	assert forall solutieCurenta :: esteSolutieValida(solutieCurenta) ==>        
	(esteSolutieOptima(solutieCurenta, rest - 50) ==> 
	esteSolutieOptima([solutieFinala[0] + solutieCurenta[0], solutieFinala[1] + solutieCurenta[1], solutieFinala[2] + solutieCurenta[2], solutieFinala[3] + solutieCurenta[3], 1 + solutieFinala[4] + solutieCurenta[4]], suma));
	
	assert  INV(rest - 50, suma, [solutieFinala[0], solutieFinala[1], solutieFinala[2], solutieFinala[3], 1 + solutieFinala[4]]);
}
\end{lstlisting}
\par 
Acesta lemă are rolul de a demonstra că prin alegerea bancnotei de 50 se va crea o soluție optimă pentru $suma$.
\par 
Știm că pentru orice soluție validă și posibilă pentru $rest$, suma dintre această soluție și $solutieFinala$ va fi o soluție posibilă pentru $suma$. Astfel, folosind un forall statement vom demonstra, similiar ca în cazul precedent, că prin însumarea secevetei $solutieFinala$ cu orice altă secvență $solutieCurenta$, care este soluție posibilă pentru $rest-50$,  vom crea o nouă soluție posibilă pentru $suma$.
\par
Următorul lucru care trebuie să îl demonstrăm este că suma elementelor celor două soluții este soluție optimă pentru $suma$. Pentru această demonstrație, am folosit un exchange argument pe care îl voi prezența în subsecțiunea următoare.



\end{enumerate}

\subsection{Exchange Arguments}
Exchange arguments este o tehnică folosită pentru a demonstra optimitatea unei soluții. Tehnica presupune modificarea unei soluții oarecare cu scopul de a obține o soluție optimă pentru agloritmul greedy fără a-i modifica costul.\par 
După cum am menționat anterior, pentru lemele $cazMaxim10$,$cazMaxim20$ și $cazMaxim50$ demonstrarea faptului că suma dintre $solutieCurenta$ și $solutieFinala$ este soluție optimă pentru $suma$ nu a mers "de la sine"ca în celalte leme, așa că va trebui "să ajutăm" verificatorul Dafny.\par 
În programul scris de mine, există două situații în care intervine folosirea acestei tehnici: 
\begin{itemize}
	\item variabila $rest$ aparține unui interval, cum ar fi $10 <= rest <20 $ si $20 <= rest < 50$
	\item variabila $rest$ are doar limită inferioară, cum ar fi $rest >= 50$.
\end{itemize}
\par 
În ceea ce urmează o să prezint câte un caz din fiecare situație. 
\par
\vspace{2cm}
\begin{enumerate}
\item Situația în care variabila $10 <= rest <20$ este reprezentată de lema exchangeArgumentCaz10(rest : int, solutieCurenta : seq$<$int$>$)
\par 
	
\begin{lstlisting}
lemma exchangeArgumentCaz10(rest : int, solutieCurenta : seq<int>)
	requires 10 <= rest < 20
	requires esteSolutieValida(solutieCurenta)
	requires esteSolutieOptima(solutieCurenta, rest - 10)
	ensures esteSolutieOptima([solutieCurenta[0], solutieCurenta[1], solutieCurenta[2] + 1, solutieCurenta[3], solutieCurenta[4]], rest)
{
	assert esteSolutie([solutieCurenta[0], solutieCurenta[1], solutieCurenta[2] + 1, solutieCurenta[3], solutieCurenta[4]], rest);
	if(!esteSolutieOptima([solutieCurenta[0], solutieCurenta[1], solutieCurenta[2] + 1, solutieCurenta[3], solutieCurenta[4]], rest))
	{
		var solutieOptima :|esteSolutieValida(solutieOptima) && esteSolutie(solutieOptima, rest) && cost(solutieOptima) < cost([solutieCurenta[0], solutieCurenta[1], solutieCurenta[2] + 1, solutieCurenta[3], solutieCurenta[4]]);
		assert cost([solutieCurenta[0], solutieCurenta[1], solutieCurenta[2] + 1, solutieCurenta[3], solutieCurenta[4]]) == cost(solutieCurenta) + 1;
		assert solutieOptima[3] == 0;
		assert solutieOptima[4] == 0;
		if(solutieOptima[2] >= 1)
		{
			var solutieOptima' := [solutieOptima[0], solutieOptima[1], solutieOptima[2] - 1, solutieOptima[3], solutieOptima[4]];
			assert esteSolutie(solutieOptima', rest - 10);
			assert cost(solutieOptima') == cost(solutieOptima) - 1;
			assert cost(solutieOptima) - 1 < cost(solutieCurenta);
			assert false;
		}
		else if(solutieOptima[1] >= 2)
		{
			var solutieOptima' := [solutieOptima[0], solutieOptima[1] - 2, solutieOptima[2], solutieOptima[3], solutieOptima[4]];
			assert esteSolutie(solutieOptima', rest - 10);
			assert cost(solutieOptima') == cost(solutieOptima) - 2;
			assert cost(solutieOptima) - 2 < cost(solutieCurenta);
			assert false;
		}else if(solutieOptima[1] >= 1 && solutieOptima[0] >= 5)
		{
			var solutieOptima' := [solutieOptima[0] - 5, solutieOptima[1] - 1, solutieOptima[2], solutieOptima[3], solutieOptima[4]];
			assert esteSolutie(solutieOptima', rest - 10);
			assert cost(solutieOptima') == cost(solutieOptima) - 6;
			assert cost(solutieOptima) - 6 < cost(solutieCurenta);
			assert false;
		}
		else if(solutieOptima[0] >= 10)
		{
			var solutieOptima' := [solutieOptima[0] - 10, solutieOptima[1], solutieOptima[2], solutieOptima[3], solutieOptima[4]];
			assert esteSolutie(solutieOptima', rest - 10);
			assert cost(solutieOptima') == cost(solutieOptima) - 10;
			assert cost(solutieOptima) - 10 < cost(solutieCurenta);
			assert false;
		}
		else{
			assert false;
		}}}
\end{lstlisting}
\setlength{\parindent}{2em}
\par
Cu ajutorul acestei leme vom demonstra faptul că soluția curentă construită astfel $[solutieCurenta[0], solutieCurenta[1], solutieCurenta[2] + 1, solutieCurenta[3],$ \ $ solutieCurenta[4]]$ este soluție optimă pentru variabila $rest$.\par
Pentru a demonstra acest lucru, vom crea o nouă soluție numită $solutieOptima$ care are cost mai mic decât soluția curentă. Știm deja că elementele \\ $solutieOptima[3]==0$ și $solutieOptima[4]==0$ așa că ne putem concentra atenția pe primele 3 elemente ale soluției. \par
În cele ce urmează, vom căuta toate combinațiile formate din numerele 1, 5 și 10 a căror sumă este 10. Pentru fiecare combinație se va crea o nouă soluție $solutieOprima'$ astfel: se vor scădea bancnotele cu valoarea respectivă din \\ $solutieOptima$. \par
De exemplu, pentru cazul $1+1+1+1+1+5=10$ noua soluție va fi $var solutieOptima' := [solutieOptima[0] - 5, solutieOptima[1] - 1, solutieOptima[2], \\ solutieOptima[3], solutieOptima[4]]$, cu alte cuvinte am scăzut 5 bancnote de valoare 1 și o bancnotă de valoare 5. \par
Știind că am scăzut $n$ bancnote din $solutieOptima$ pentru a crea $solutieOptima'$ costul acesteia va fi $cost(solutieOptima) - n$, iar încercând să comparăm acest cost cu costul soluției curente vom ajunge la o contradicție.
\vspace{1cm}
\item Situația în care variabila $ rest <= 50$ este reprezentată de lema exchangeArgumentCaz50(rest : int, suma : int, solutieOarecare : seq$<$int$>$, solutieCurenta : seq$<$int$>$)
\par 
Codul pentru această lema este foarte lung și nu îl voi insera pe tot în acest document. Voi prezența anumite instrucțiuni care au o importantă deosebită, iar secvențele repetitive le voi înlocui cu un comentariu sugestiv.
\par 
Acesta lemă are drept scop demonstrarea faptului că variabila $solutieCurenta$ are un cost mai mic decât costul variabilei $solutieOarecare$.

\begin{lstlisting}
assert esteSolutie(solutieOarecare, rest);
assert esteSolutie([solutieCurenta[0], solutieCurenta[1], solutieCurenta[2], solutieCurenta[3], 1 + solutieCurenta[4]], rest);
if(cost(solutieOarecare) < cost([solutieCurenta[0], solutieCurenta[1], solutieCurenta[2], solutieCurenta[3], 1 + solutieCurenta[4]]))
{
	if(solutieOarecare[4] > solutieCurenta[4] + 1)
	{
		assert cost([solutieOarecare[0], solutieOarecare[1], solutieOarecare[2], solutieOarecare[3], solutieOarecare[4] - 1]) < cost(solutieCurenta);
		assert esteSolutieOptima([solutieOarecare[0], solutieOarecare[1], solutieOarecare[2], solutieOarecare[3], solutieOarecare[4] - 1], rest - 50);
		assert false;
	}
	else if(solutieOarecare[4] < solutieCurenta[4] + 1)
	{
		assert (solutieOarecare[0] + (5 * solutieOarecare[1])+(10 * solutieOarecare[2]) + (20 * solutieOarecare[3])) >= 50;
		
		if(solutieOarecare[2] >= 1 && solutieOarecare[3] >= 2)
		{
			var nouaSolutieOarecare := [solutieOarecare[0], solutieOarecare[1], solutieOarecare[2] - 1, solutieOarecare[3] - 2, solutieOarecare[4] + 1];
			exchangeArgumentCaz50(rest, suma, nouaSolutieOarecare, solutieCurenta);
		}
		//combinatiile de 1,5,10 si 20 a caror suma este 50
		else{
			assert solutieOarecare[0] >= 0;
			assert solutieOarecare[1] >= 0;
			assert solutieOarecare[2] >= 0;
			assert solutieOarecare[3] >= 3;
			if(solutieOarecare[3] >= 3)
			{
				var nouaSolutieOarecare := [solutieOarecare[0], solutieOarecare[1], solutieOarecare[2] + 1, solutieOarecare[3] - 3, solutieOarecare[4] + 1];
				assert cost(nouaSolutieOarecare) < cost(solutieOarecare);
				exchangeArgumentCaz50(rest, suma, nouaSolutieOarecare, solutieCurenta);
			}
		}
	}
	assert solutieOarecare[4] == (solutieCurenta[4] + 1);

\end{lstlisting}
\par
Începem demonstrația prin a verifica dacă $solutieOarecare$ are costul mai mic decât $cost(solutieCurenta)$. În caz afirmativ, vom începe să comparăm elementele din cele două soluții: $solutieOarecare[4] > solutieCurenta[4] + 1$ ,\\ $solutieOarecare[3] > solutieCurenta[3]$, $solutieOarecare[2] > solutieCurenta[2]$, $solutieOarecare[1] > solutieCurenta[1]$ și $solutieOarecare[0] > solutieCurenta[0]$.  \par   
Secventa de cod adăugată mai sus conține doar compararea elementelor de pe poziția 4 deoarece această este cea mai complexă parte. \par
Dacă $solutieOarecare[4] > solutieCurenta[4] + 1$  atunci soluția \\ $[solutieOarecare[0], solutieOarecare[1], solutieOarecare[2], solutieOarecare[3], \\ solutieOarecare[4] - 1]$ are costul mai mic decât costul soluției curente și este soluție optimă pentru $rest-50$, lucru care este fals.\par
În schimb, dacă $solutieOarecare[4] < solutieCurenta[4] + 1$ vom crea o nouă soluție numită $nouaSolutieOarecare$ formată din elementele secvenței $solutieOarecare$ din care am scăzut bancnotele a căror sumă este egală cu 50 și am incrementat valoarea elementului de pe poziția 4, apoi, cu ajutorul unui apel recursiv, vom compara $nouaSolutieOarecare$ cu $solutieCurenta$ până când acestea vor fi egale.\par
Similar se întâmplă și cu celelalte trei comparații pe care le-am enumerat mai sus. Se vor crea noi soluții până când elementele din $solutieOarecare$ și $solutieCurenta$ vor fi egale.
\end{enumerate}
\vspace{1cm}



\section{Probleme "interesante" intalnite}
Pe parcursul realizării acestui algoritm și a demonstrării corectitudinii sale am întâmpinat mai multe probleme care m-au pus în dificultate. \par  
Problema care m-a "încurcat" cel mai mult am întâmpinat-o la lema exchangeArgumentCaz50(rest: int, suma : int, solutieOarecare : seq$<$int$>$, solutieCurenta : seq$<$int$>$). După cum am prezentat anterior, pentru a demonstra că $solutieOarecare[4] == solutieCurenta[4] + 1$, cu alte cuvinte, dacă ambele soluții conțineau același număr de bancnote de valoare 50, a trebuit să caut toate combinațiile de valori de 1,5,10 și 20 a căror suma este 50 și să creez o nouă soluție. \par 
\begin{itemize}
	\item Problema 1: După ce am adăugat în lemă toate cele 56 de cazuri posibile, lema nu putea să demonstreze că $solutieOarecare[4] == solutieCurenta[4] \\ + 1$, ceea ce însemna că îmi lipsește un caz.\par
	Rezulvarea problemei 1: În ultima instrucțiune $else$ am adăugat câteva assert-uri pentru a afla ce caz sau cazuri am ratat. Spre surprinderea mea,\\ $solutieOarecare[0]==solutieOarecare[1]==solutieOarecare[2]==0$ iar \\ $solutieOarecare[3]==3$  asta însemna că aveam 60 de lei formați din 3 bancnote de 20 de lei.
	\item Problema 2: Cum aș putea să tratez acest caz?\par 
	Încercarea 1: Am încercat să verific dacă $solutieOarecare[3]>=5$ astfel încât să pot adaugă 2 bancnote de 50 de lei $solutieOarecare[4]+2$ $\rightarrow$ eroare: $solutieOarecare[3]$ nu este mai mare sau egal cu 5.\par
	Încercarea 2: Am încercat să verific dacă $ solutieOarecare[0]+ 5 \cdot solutieOarecare[1]+10\cdot solutieOarecare[2] >=100$ pentru a putea adaugă valoarea 5 la $solutieOarecare[3]$ și să pot reveni la ideea de la "Încercarea 1" $\rightarrow$ eroare: suma elementelor nu este mai mare sau egală cu 100. \par
	Încercare 3: Am încercat să tratez cazul astfel: cele 3 bancnote de 20 le voi transforma într-o bancnotă de 50 iar bancnota de 10 lei rămasă o voi aduna la celalate pe care le am în soluție, astfel, creând o nouă soluție cu un cost mai mic \\
	($nouaSolutieOarecare := [solutieOarecare[0], solutieOarecare[1], solutieOarecare[2] \\ + 1, solutieOarecare[3] - 3, solutieOarecare[4] + 1]$) $\rightarrow$ eroare: "decreases clase might not decrease"
	\item Problema 3: Eroarea "decreases clase might not decrease" apăruse deoarece în adnotările lemei inițial exista $decreases \ solutieOarecare[0], solutieOarecare[1],\\ solutieOarecare[2],solutieOarecare[3]$ ceea ce ne asigura că elementele secvenței $solutieOarecare$ vor descrește. În încercarea mea de a trata cazul rămas, voiam să adaug "restul" în $solutieOarecare[2]$ și încălcam adnotarea specificată mai sus.
	\par 
	Rezolvarea problemei 2: Cum scopul pricipal al lemei era de a demonstra că $solutieCurenta$ are cost minim am modificat adnotarea astfel $decreases  \\ solutieOarecare[0] + solutieOarecare[1] + solutieOarecare[2] + solutieOarecare[3]$ deoarece este suficient să descrească suma acestora pentru a ajunge la o soluție cu cost mai mic.
		
\end{itemize}





